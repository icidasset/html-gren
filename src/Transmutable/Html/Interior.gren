module Transmutable.Html.Interior exposing ( Html(..), Attribute(..), NodeProperties, Descendants(..), EventDecoder(..), map, transmute )

{-| This module contains the core building blocks that make up the AST.

âš ï¸ This module should only be used by code that transmutes HTML. If you just want to write HTML, use the main `Html` module and the other modules such as `Html.Attributes`.

# Types

@docs Html, Attribute, EventDecoder, NodeProperties, Descendants

# Transmute

@docs transmute

# Mapping

@docs map

-}

import Json.Decode exposing ( Decoder, Value )



-- ROOT


{-|-}
type Html msg
    = Node NodeProperties (Array (Attribute msg)) (Descendants msg)
    | TextNode String



-- PARTS


{-|-}
type alias NodeProperties =
    { key : Maybe String
    , tagName : String
    }


{-|-}
type Attribute msg
    = Attribute String String
    | BoolProperty String Bool
    | Event String (EventDecoder msg)
    | StringProperty String String
    | Style String String
    | ValueProperty String Value


{-|-}
type Descendants msg
    = Regular (Array (Html msg))


{-|-}
type EventDecoder msg
    = Normal (Decoder msg)
    | MayStopPropagation
        (Decoder
            { message : msg
            , stopPropagation : Bool
            }
        )
    | MayPreventDefault
        (Decoder
            { message : msg
            , preventDefault : Bool
            }
        )
    | Custom
        (Decoder
            { message : msg
            , stopPropagation : Bool
            , preventDefault : Bool
            }
        )



-- ðŸ› ï¸


{-|-}
map : (a -> b) -> Html a -> Html b
map f node =
    case node of
        Node props attrs descendants ->
            Node props (Array.map (mapAttribute f) attrs) (mapDescendants f descendants)

        TextNode content ->
            TextNode content


mapAttribute : (a -> b) -> Attribute a -> Attribute b
mapAttribute fn attribute =
    case attribute of
        Attribute key value ->
            Attribute key value

        Event name eventDecoder ->
            Event name (mapEventDecoder fn eventDecoder)

        BoolProperty key value ->
            BoolProperty key value

        StringProperty key value ->
            StringProperty key value

        Style key value ->
            Style key value

        ValueProperty key value ->
            ValueProperty key value


mapDescendants : (a -> b) -> Descendants a -> Descendants b
mapDescendants fn descendants =
    case descendants of
        Regular nodes ->
            Regular (Array.map (map fn) nodes)


mapEventDecoder : (a -> b) -> EventDecoder a -> EventDecoder b
mapEventDecoder fn eventDecoder =
    case eventDecoder of
        Normal d ->
            Normal (Json.Decode.map fn d)

        MayStopPropagation d ->
            MayStopPropagation
                (Json.Decode.map
                    (\a ->
                        { message = fn a.message
                        , stopPropagation = a.stopPropagation
                        }
                    )
                    d
                )

        MayPreventDefault d ->
            MayPreventDefault
                (Json.Decode.map
                    (\a ->
                        { message = fn a.message
                        , preventDefault = a.preventDefault
                        }
                    )
                    d
                )

        Custom d ->
            Custom
                (Json.Decode.map
                    (\v ->
                        { message = fn v.message
                        , stopPropagation = v.stopPropagation
                        , preventDefault = v.preventDefault
                        }
                    )
                    d
                )


{-| Use this helper function to translate `Html` into another data type.
-}
transmute :
    { node :
        NodeProperties
        -> Array (Attribute msg)
        -> { namespace : Maybe String
           }
        -> outcome
    , injectDescendants : outcome -> Array transmuted -> transmuted
    , text : String -> transmuted
    }
    -> Html msg
    -> transmuted
transmute =
    transmute_
        { namespace = Nothing
        }


transmute_ :
    { namespace : Maybe String
    }
    -> { node :
            NodeProperties
            -> Array (Attribute msg)
            -> { namespace : Maybe String
               }
            -> outcome
       , injectDescendants : outcome -> Array transmuted -> transmuted
       , text : String -> transmuted
       }
    -> Html msg
    -> transmuted
transmute_ context manager html =
    case html of
        Node props attributes descendants ->
            let
                xmlns =
                    -- Check for a `xmlns` attribute which will change the active namespace
                    attributes
                        |> Array.findLast
                                (\attribute ->
                                    case attribute of
                                        Attribute "xmlns" ns ->
                                            True

                                        _ ->
                                            False
                                )
                        |> Maybe.andThen
                                (\attribute ->
                                    case attribute of
                                        Attribute "xmlns" ns ->
                                            Just ns

                                        _ ->
                                            Nothing
                                )

                newContext =
                    -- If the `xmlns` attribute is present,
                    -- the active XML namespace will change.
                    case xmlns of
                        Just ns ->
                            { context | namespace = Just ns }

                        Nothing ->
                            context
            in
            manager.injectDescendants
                (manager.node props attributes newContext)
                (case descendants of
                    Regular array ->
                        Array.map (transmute_ newContext manager) array
                )

        TextNode text ->
            manager.text text
